with Agents;
with Commodities;
with Factions;
with Money;
with Pop_Groups;
with Pops;
with Worlds;
with World_Sectors;

package Colonies is

record Colony : Agent, Has_Stock, Managed is
   key World;
   key Faction;
   Capital   : World_Sector;
   Plurality : Long_Float;
end Colony;

record Colony_Sector is
   key Colony;
   unique key World_Sector;
end Colony_Sector;

record Colony_District is
   key Colony;
   key World_Sector;
   District;
   Size : Long_Float;
   key Colony_District with Colony, District;
   key Colony_Sector_District with Colony, World_Sector, District;
end Colony_District;
   
record Colony_Pop_Group is
   key Colony;
   Pop_Group;
   Size   : Long_Float;
   Income : Money_Type;
   unique key Colony_Pop_Group with Colony, Pop_Group;
end Colony_Pop_Group;

record Colony_Price is
   key Colony;
   key Commodity;
   Price : Price_Type;
   unique key Commodity_Price with Colony, Commodity;
end Colony_Price;

end Colonies;
