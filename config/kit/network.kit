package Network is

type Node_Value_Type is (Rating, Money, Quantity);

record Node : Localised is
   Content        : Node_Value_Type;
end Node;

record Calculation is
   key Node;
   Add       : Long_Float;
   Multiply  : Long_Float;
   Exponent  : Long_Float;
   Inertia   : Long_Float;
end Calculation;

record Effect : Calculation is
   key To    : Node;
end Effect;

record Metric : Node is
end Metric;

record Pop_Group : Localised is
   key Is_Wealth_Group : Boolean;
   Proportion          : Long_Float;
end Pop_Group;

record Group_Influence is
   key From  : Pop_Group;
   key To    : Pop_Group;
   Influence : Long_Float;
   unique key Group_Influence with From, To;
end Group_Influence;

record Policy : Node is
   key Internal : Boolean;
   Min_Cost : Long_Float;
   Max_Cost : Long_Float;
end Policy;

record Cost_Multiplier : Calculation is
   key Policy;
end Cost_Multiplier;

record Value_Multiplier : Calculation is
   key Policy;
end Value_Multiplier;

record Sector_Use : Localised is
   Parent : Sector_Use;
end Sector_Use;
   
record Zone : Commodity, Node is
   key Sector_Use;
end Zone;

record Economic_Sector : Node is
   key Zone;
end Economic_Sector;

record Network;

record Network_Value is
   key Network;
   key Node;
   Active         : Boolean;
   Changed        : Boolean;
   Real_Value     : Long_Float;
   New_Value      : Long_Float;
   Old_Value      : Long_Float;
   Changed_At     : Time;
   unique key Network_Value with Network, Node;
end Network_Value;

end Network;
